#version 400

layout (triangles) in;
layout (triangle_strip, max_vertices=3) out;

// Input vertex data, aggregated into triangles
in vec3 normal_tes[];
in vec3 position_tes[];
in vec3 eye_tes[];
in vec3 light_tes[];
in vec2 uv_tes[];

// Output data per vertex, passed to primitive assembly and rasterization
out vec3 normal_gs;
out vec3 eye_gs;
out vec3 light_gs;
out vec2 uv_gs;

// Uniform values that stay constant for the whole mesh.
uniform sampler2D displacementTexture;
uniform float time;
uniform mat4 MVP;
uniform mat4 P;
uniform mat4 V;


vec3 GetNormal(vec4 a, vec4 b, vec4 c)
{
   vec3 x = b.xyz - a.xyz;
   vec3 y = c.xyz - b.xyz;
   return normalize(cross(x, y));
}


vec3 Gerstner(vec3 worldpos, float w, float A, float phi, float Q, vec2 D, int N) {
	float newQ = Q / (w*A*N);
	return vec3((newQ*A*D.x*cos(dot(w*D, worldpos.xz) + (phi*time))), (A*sin(dot(w*D, worldpos.xz) + (phi*time))), (newQ*A*D.y*cos(dot(w*D, worldpos.xz) + (phi*time))));
}


void main() {

	vec4 pos[gl_in.length()];

	for(int i = 0; i < gl_in.length(); ++i) {
		pos[i] = vec4(position_tes[i], 1.0);
		pos[i] += vec4(Gerstner(position_tes[i], 4, 0.08, 1.1, 0.75, vec2(0.3, 0.6), 4), 0.0);
		pos[i] += vec4(Gerstner(position_tes[i], 2, 0.05, 1.1, 0.75, vec2(0.2, 0.866), 4), 0.0);
		pos[i] += vec4(Gerstner(position_tes[i], 0.6, 0.2, 0.4, 0.1, vec2(0.3, 0.7), 4), 0.0);
		pos[i] += vec4(Gerstner(position_tes[i], 0.9, 0.15, 0.4, 0.1, vec2(0.8, 0.1), 4), 0.0);
	}

	vec3 norm = GetNormal(pos[0], pos[1], pos[2]);

	for(int i = 0; i < gl_in.length(); ++i) {
		float displace = texture(displacementTexture, uv_tes[i]).y;
		pos[i].y += displace;
		gl_Position = MVP * pos[i];
		uv_gs = uv_tes[i];
		eye_gs = eye_tes[i];
		light_gs = light_tes[i];
		normal_gs = norm;
		EmitVertex();
	}
	EndPrimitive();

}

